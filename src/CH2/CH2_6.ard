\import CH1.Def.def (xx)
\import CH2.CH2_1
\import CH2.CH2_2
\import CH2.CH2_3
\import CH2.CH2_4
\import CH2.CH2_5

-- 2.6 Cartesian Product Types

{- Given types A and B, consider the product type A xx B. For any elements x, y : A xx B, we are going to show that
the canonical projection maps (x = y) -> (x.1 = y.1) xx (x.2 = y.2) is an equivalence.
 -}

-- first we actually construct the canonical maps:

\func pr1 {A B : \Type} : A xx B -> A => \lam x => x.1

\func pr2 {A B : \Type} : A xx B -> B => \lam x => x.2

\func pr1= {A B : \Type} {a b : A xx B} (p : a = b) : a.1 = b.1 => ap pr1 p

\func pr2= {A B : \Type} {a b : A xx B} (p : a = b) : a.2 = b.2 => ap pr2 p

\func xx=func {A B : \Type} {x y : A xx B} (p : x = y) : (x.1 = y.1) xx (x.2 = y.2) => (ap pr1 p, ap pr2 p)

-- now we are going to find an inhabitant of isequiv xx=func, the first step is find the inverse map:

\func xx=inverse' {A B : \Type} {x y : A xx B} (r : x.1 = y.1) (s : x.2 = y.2) : x = y \elim x, y, r, s
  | (a,b) , (c,d) , idp, idp => idp

\func xx=inverse {A B : \Type} {x y : A xx B} (q : (x.1 = y.1) xx (x.2 = y.2) ) : x = y => xx=inverse' q.1 q.2

-- then we will prove xx=inverse o xx=func ~ id.

\func xx=unit' {A B : \Type} (x y : A xx B) (p : x = y) : (xx=inverse o xx=func) (p)= p \elim x,  p
  | (a_1, b_1), idp => idp

\func xx=unit {A B : \Type} {x y : A xx B} :  (xx=inverse o xx=func) ~ {x = y} {\lam p => x = y} id => \lam p =>
    xx=unit' x y p

-- now for the converse xx=func o xx=inverse ~ id:

\func xx=counit' {A B : \Type} (x y : A xx B) (r : (x.1 = y.1)) (s : (x.2 = y.2)) : xx=func (xx=inverse' r s)  =  (r,s)
 \elim x, y, r, s | (a_1, b_1) , (a_2, b_2), idp, idp => idp

\func xx=counit {A B : \Type} {x y : A xx B} (q : (x.1 = y.1) xx (x.2 = y.2)) : (xx=func o xx=inverse) q = q
  \elim q | (r,s) => xx=counit' x y r s

-- now putting them altogher:

\func qinvxx=func {A B : \Type} {x y : A xx B} : qinv (xx=func {A}{B}{x}{y}) => (xx=inverse, xx=counit, xx=unit)

\func isequivxx=func {A B : \Type} {x y : A xx B} : isequiv (xx=func{A}{B}{x}{y}) => qinv_to_isequiv (xx=func) (qinvxx=func)

\func xx=ISO {A B :\Type} {x y : A xx B} : (x = y) ISO ((x.1 = y.1) xx (x.2 = y.2)) => (xx=func, isequivxx=func)

{-  note that we can think about xx=inverse, which we will also denote as pair=, can be viewed as a constructor
for x = y. By the same note, we have xx=func as the eliminator, and the two homotopies plays role of the propositional
computation rules and the propositional uniqueness principle. This is another way to view about we are doing,
which is we are propositionally (doing it in the type theory) constructing the type x = y for x, y : A xx B
 -}

\func pair= {A B : \Type} {x y : A xx B} (q : (x.1 = y.1) xx (x.2 = y.2)) : x = y => xx=inverse q

-- now considering a tranport in a pointwise product of type families:

\func \infix 2 XX {Z : \Type} (A B : Z -> \Type) : Z -> \Type => \lam z => (A z) xx (B z)

-- given p : z = w, x : A(z) xx B(z), we can transport x along p to get something in A(w) xx B(x).

\func xxxtransport {Z : \Type} {z w : Z} (A B : Z -> \Type) (p : z = w) (x : A(z) xx B(z)) :
transport (A XX B) p x = (transport A p x.1 , transport B p x.2) \elim p, x | idp, (a,b) => idp

-- now we can consider functoriality of ap under Cartesian products.

\func \infix 2 XxX {A B A' B' : \Type} (g : A -> A')(h : B -> B') : A xx B -> A' xx B' => \lam x => (g x.1, h x.2)

\func ap_with_product {A B A' B' : \Type} {x y : A xx B} (g : A -> A')(h : B -> B')(r : x.1 = y.1) (s : x.2 = y.2) :
ap (g XxX h) (pair= (r,s)) = pair= (ap g r , ap h s) \elim x, r, s | (a,b), idp ,idp => idp