\import CH2.Chpt.sec01
\import CH1.Def.comp
\import CH1.Def.path

-- 2.2 Functions are functors

{- we are going to see how functions behave with paths. We will see that all functions takes paths
to paths, and higher paths to higher paths. Topologically, we see that all functions are "continuous" -}

-- functions take paths to paths, and it takes reflexivity to reflexivity:

\func ap {A B : \Type} {x y : A} (f : A -> B) (p : x = y) : f x = f y \elim p
  | idp => idp \where
{
  -- recall that we used ap_succ => ap succ in CH1.Chpt.sec09

  -- function ap is called application, or action on paths.

  -- basics lemmas about ap f, how it behave with composition and inverses:

  \func distribute_* {A B : \Type} {x y z : A}
                     (p : x = y) (q : y = z) (f : A -> B) :
    ap f (p * q) = ap f p * ap f q \elim p,q
    | idp, idp => idp

  \func distribute_inv {A B : \Type} {x y : A}
                       (p : x = y) (f : A -> B) :
    ap f (inv p) = inv (ap f p) \elim p
    | idp => idp

  -- recall definition of composition of functions from CH1.Ex.ex01

  \func distribute_o {A B C : \Type} {x y : A}
                     (p : x = y) (f : A -> B) (g : B -> C) :
    ap (g o f) p = (ap g o ap f) p  \elim p
    | idp => idp

  -- Recall the identity function

  \func involutive_id {A : \Type} {x y : A}
                      (p : x = y) :
    ap id p = p \elim p
    | idp => idp
  -- note that these rules themselves have higher coherence laws.
}


